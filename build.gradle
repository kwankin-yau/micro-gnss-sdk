plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.lucendar'
version '3.4.4'

sourceCompatibility = 17
targetCompatibility = 17

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.checkerframework:checker-qual:3.36.0'
    implementation('com.google.code.gson:gson:2.10.1')
    implementation("com.squareup.okhttp3:okhttp:4.11.0")
    implementation("com.squareup.okhttp3:okhttp-sse:4.11.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.11.0")
    implementation("org.slf4j:slf4j-api:2.0.7")

    implementation("org.springframework:spring-webmvc:6.0.10")
    implementation("org.springframework:spring-websocket:6.0.10")
    implementation("org.springframework:spring-messaging:6.0.10")


    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'


    implementation("com.lucendar:lucendar-common:2.0.0")
    implementation("com.lucendar:jt808-common:3.0.0")
    implementation("com.lucendar:strm-sdk:3.4.4")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}


java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Micro-GNSS SDK library'
                packaging = 'jar'
                // optionally artifactId can be defined here
                description = 'GT-Streaming 媒体服务中间件的Micro-GNSS接口库.'
                url = 'https://github.com/kwankin-yau/micro-gnss-sdk'

                scm {
                    connection = 'https://github.com/kwankin-yau/micro-gnss-sdk.git'
                    developerConnection = 'https://github.com/kwankin-yau/micro-gnss-sdk.git'
                    url = 'https://github.com/kwankin-yau/micro-gnss-sdk'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'kky'
                        name = 'kwankin-yau'
                        email = 'alphax@vip.163.com'
                    }
                }
            }
        }
    }
}

javadoc {
    options.encoding = 'UTF-8'

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    sign publishing.publications.maven
}

